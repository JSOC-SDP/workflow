#! /bin/csh -f
#
# these lines for all products
if ($?WORKFLOW_ROOT) then
  set WFDIR = $WORKFLOW_DATA
  set WFCODE = $WORKFLOW_ROOT
else
  echo Need WORKFLOW_ROOT variable to be set.
  exit 1
endif

cd $WFDIR
rm -f Keep_running
while (-e GATEKEEPERBUSY)
  echo waiting for gatekeeper to quit.
  sleep 5
  end

# begin section for this product

# make test gates and tasks for HMI Vector Field and other 720s observables
# This includes the several input series.

# remove prior versions

cd $WFDIR/gates
rm -rf hmi.Vector 

cd $WFDIR/tasks
rm -rf update_hmi.Vector 

# Make tasks and set preconditions

# NOTE the "command" script used below must be available and executable when this script is run
cd $WFDIR
$WFCODE/maketask.csh task=update_hmi.Vector manager=taskmanager.csh target=hmi.Vector \
		command=scripts/update_hmi.Vector.csh maxrange=2160 parallelOK=8

set PRECOND = $WFDIR/tasks/update_hmi.Vector/preconditions

mkdir $PRECOND/hmi.cosmic_rays
cat > $PRECOND/hmi.cosmic_rays/prepare_ticket <<STOP
    set ACTION = 3
    set SPECIAL = CAMERA=1
    set TMPLOW_t = \`time_convert time=\$USELOW\`
    @   TMPLOW_t = \$TMPLOW_t - 750
    set USELOW = \`time_convert zone=TAI s=\$TMPLOW_t\`
    set TMPHIGH_t = \`time_convert time=\$USEHIGH\`
    @   TMPHIGH_t = \$TMPHIGH_t + 750
    set USEHIGH = \`time_convert zone=TAI s=\$TMPHIGH_t\`
STOP

mkdir $PRECOND/hmi.V_drift_coefs
cat > $PRECOND/hmi.V_drift_coefs/prepare_ticket <<STOP
    set ACTION = 5
    set TMPLOW_t = \`time_convert time=\$USELOW\`
    @   TMPLOW_t = \$TMPLOW_t - 86400
    set USELOW = \`time_convert zone=TAI s=\$TMPLOW_t\`
    set TMPHIGH_t = \`time_convert time=\$USEHIGH\`
    @   TMPHIGH_t = \$TMPHIGH_t + 750
    set USEHIGH = \`time_convert zone=TAI s=\$TMPHIGH_t\`
STOP

# Prerequisite tasks made in LOS add script

# Now make any user gates.

# NOTE hmi.S_720s is made at the same time as M, V, Ic, Ld, and Lw so only
# one product needs to be tracked here.

cd $WFDIR
$WFCODE/makegate.csh gate_name=hmi.Vector product=hmi.S_720s type=time key=T_REC project=HMI \
		 actiontask=update_hmi.Vector statustask=scripts/status-general.csh

# Prerequisite gates made in LOS add script

# initialize the gates
$WFCODE/maketicket.csh gate=hmi.Vector wantlow=NaN wanthigh=NaN action=6

