#! /bin/csh -f
# Script to make HMI Ic_noLimbDark data, either 45s or 720s
#

# XXXXXXXXXX test
 set echo
# XXXXXXXXXX test

set HERE = $cwd 

if ($?WORKFLOW_ROOT) then
  set WFDIR = $WORKFLOW_DATA
  set WFCODE = $WORKFLOW_ROOT
else
  echo Need WORKFLOW_ROOT variable to be set.
  exit 1
endif

if ( $JSOC_MACHINE == "linux_x86_64" ) then
  set QUE = j.q,p.q
  set QSUB = qsub
else if ( $JSOC_MACHINE == "linux_avx" ) then
  set QUE = a.q
  set QSUB = qsub2
endif

foreach ATTR (WANTLOW WANTHIGH GATE)
   set ATTRTXT = `grep $ATTR ticket`
   set $ATTRTXT
end
set SPECIAL = `grep SPECIAL ticket`
if ($#SPECIAL > 0) then
  set $SPECIAL
else
  set SPECIAL
endif

set product = `cat $WFDIR/gates/$GATE/product`
set key = `cat $WFDIR/gates/$GATE/key`

set HMI_limbdark = /home/jsoc/cvs/Development/JSOC/bin/$JSOC_MACHINE/hmi_limbdark

set LD_args = "-cnxf $SPECIAL"

if ($product == hmi.Ic_noLimbDark_720s || $product == hmi.Ic_noLimbDark_720s_nrt) then
   set suffix = "720s"
else
   set suffix = "45s"
endif
if ($product =~ '*_nrt') then
   set suffix = $suffix'_nrt'
endif

# round times to a slot
set timestr = `echo $WANTLOW  | sed -e 's/[.:]//g' -e 's/^......//' -e 's/.._TAI//'`
set timename = nLD
set qsubname = $timename$timestr

set TEMPLOG = $HERE/runlog
set TEMPCMD = $HERE/$qsubname
echo 6 > $HERE/retstatus

# make qsub script
echo "#! /bin/csh -f " >$TEMPCMD
echo "cd $HERE" >>$TEMPCMD
echo "hostname >>&$TEMPLOG" >>$TEMPCMD
echo "set echo >>&$TEMPLOG" >>$TEMPCMD
echo 'set retstatus=0' >>&$TEMPCMD
echo "$HMI_limbdark in=hmi.Ic_"$suffix"\["$WANTLOW"-"$WANTHIGH"]" out=$product $LD_args ">>&$TEMPLOG" >>$TEMPCMD
echo 'set retstatus = $?' >>$TEMPCMD
echo 'echo $retstatus >retstatus' >>$TEMPCMD

# execute qsub script
set TEMPLOG = `echo $TEMPLOG | sed "s/^\/auto//"`
$QSUB -sync yes -e $TEMPLOG -o $TEMPLOG -q $QUE $TEMPCMD >> runlog

if (-e retstatus) set retstatus = `cat $HERE/retstatus`
exit $retstatus
