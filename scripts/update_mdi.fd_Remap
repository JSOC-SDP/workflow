#! /bin/csh -f

set first = `cat wantlow`
set last = `cat wanthigh`

if ( $JSOC_MACHINE == "linux_x86_64" ) then
  set QUE = j.q
  set QSUB = qsub
else if ( $JSOC_MACHINE == "linux_avx" ) then
  set QUE = a.q
  set QSUB = /SGE2/bin/lx-amd64/qsub
endif

set HERE = $cwd
set LOG = $HERE/runlog
set CMD = $HERE/MDIremap
set babble = $HERE/babble

date >> $LOG

# make qsub script
echo "#! /bin/csh -f " >$CMD
echo "cd $HERE" >>$CMD
echo "hostname >>& $LOG" >>$CMD

## README!
##########################################################################################
## jv2helio deprecated and changed to jv2ts on 2013.02.05.  More changes will be needed ##
#########################################################################################

#echo "jv2helio in='mdi.fd_M_96m_lev18['$first'-'$last']' out=mdi.fd_M_96m_remap MOFFSET=1 MCORLEV=1 VCORLEV=0 MAPRMAX=0.994 MAPLGMAX=90 MAPLGMIN=-90 MAPBMAX=90 LGSHIFT=2 MAPMMAX=1800 SINBDIVS=540 CARRSTRETCH=1 >>& $LOG" >>$CMD

echo "jv2ts in='mdi.fd_M_96m_lev18['$first'-'$last']' v2hout=mdi.fd_M_96m_remap MOFFSET=1 MCORLEV=1 VCORLEV=0 MAPRMAX=0.994 MAPLGMAX=90 MAPLGMIN=-90 MAPBMAX=90 LGSHIFT=2 MAPMMAX=1800 SINBDIVS=540 CARRSTRETCH=1 >>& $LOG" >>$CMD
echo 'set retstatus = $?' >>$CMD
echo 'echo $retstatus >' "$HERE/retstatus" >>$CMD
echo "rm -f $HERE/qsub_running" >>$CMD

# execute qsub script
touch $HERE/qsub_running
set LOG = `echo $LOG | sed "s/^\/auto//"`
$QSUB -e $LOG -o $LOG -q $QUE $CMD

set waitcount=0
while (-e $HERE/qsub_running)
    sleep 10
    @ waitcount = $waitcount + 1
    if ($waitcount > 720) then
      echo "timed out after 2 hours" >$HERE/FailMessage
      set retstatus = 5
      exit $retstatus
    endif
    echo -n '+' >>$babble
end

set retstatus = `cat $HERE/retstatus`
exit $retstatus
