# /bin/csh -f
# create limb darkening removed Ic record and create image set for Ic_noLD and M for range of time
# specified by first two arguments.

# set echo

set wantlow = $1
set wanthigh = $2

# from here make script that can make _nrt images in standard place

set CADENCE = 15

set wantlow_t = `time_convert time=$wantlow`
set wanthigh_t = `time_convert time=$wanthigh`

@ daylow = $wantlow_t / 86400
@ dayhigh = $wanthigh_t / 86400

set end_movie_t = 0

set day = $daylow
while ($day <= $dayhigh)
  @ day_t = 86400 * $day
  @ nextday_t = $day_t + 86400
  @ last_t = $nextday_t - 45
  if ($last_t > $wanthigh_t) set last_t = $wanthigh_t
  set first_t = $day_t
  if ($first_t < $wantlow_t) set first_t = $wantlow_t
  @ cadence = $CADENCE * 60
  @ first_mod_t = $first_t - 45
  @ imglow_t = $first_mod_t / $cadence
  @ imglow_t = $imglow_t + 1
  @ first_t = $imglow_t * $cadence
  set first = `time_convert s=$first_t zone=TAI`
  @ last_mod_t = $last_t + $cadence
  @ imghigh_t = $last_mod_t / $cadence
  @ n_images = $imghigh_t - $imglow_t
  if ($n_images < 0) set n_images = 0
  if ($n_images == 0) then
    @ day = $day + 1
    continue
  endif
  set end_movie_t = $last_t
  @ n_minutes = $n_images * $CADENCE
  set QRY = '['$first'/'$n_minutes'm@'$CADENCE'm]'
  set yyyymmdd = `echo $first | sed -e 's/_.*//' -e 's/\./Q/' -e 's/\./X/'`
  set YEAR = `echo $yyyymmdd | sed -e 's/Q.*//'`
  set MON = `echo $yyyymmdd | sed -e 's/^.*Q//' -e 's/X.*//'`
  set DAY = `echo $yyyymmdd | sed -e 's/^.*X//'`

  set IMGPATH = /home/jsoc/hmi/images/$YEAR
  if (!(-e $IMGPATH)) mkdir $IMGPATH
  set IMGPATH = $IMGPATH/$MON
  if (!(-e $IMGPATH)) mkdir $IMGPATH
  set IMGPATH = $IMGPATH/$DAY
  if (!(-e $IMGPATH)) mkdir $IMGPATH

  set n_source = `show_info -cq 'hmi.Ic_45s_nrt'$QRY`
  set n_dest = `show_info -cq 'hmi.Ic_noLimbDark_45s'$QRY`
  if ($n_source > 0 && $n_dest < $n_source) then
    hmi_limbdark in='hmi.Ic_45s_nrt'$QRY out=hmi.Ic_noLimbDark_45s -n
  endif

  cd $IMGPATH

  setenv RGBDEF /home/phil/workflow/scripts/rgb.txt

  render_image in='hmi.Ic_45s_nrt'$QRY \
    pallette=/home/phil/apps/heat.sao \
    min=20000 \
    max=75000 \
    scale=1,4,8,16 \
    outid=time \
    type=jpg \
    outname=Ic \
    out='| ppmlabel -color white -size {%0.75:5} -x 15 -y {%98} -text "SDO/HMI Quick-Look Continuum: {ID}" | pnmtojpeg -quality=95'

  render_image in='hmi.Ic_noLimbDark_45s'$QRY \
    pallette=/home/phil/apps/heat.sao \
    min=0.3 \
    max=1.2 \
    scale=1,4,8,16 \
    outid=time \
    type=jpg \
    outname=Ic_flat \
    out='| ppmlabel -color white -size {%0.75:5} -x 15 -y {%98} -text "SDO/HMI Quick-Look Continuum: {ID}" | pnmtojpeg -quality=95'

  render_image in='hmi.M_45s_nrt'$QRY \
    outname=M \
    pallette=/home/phil/apps/mag.lut \
    outid=time \
    -c \
    min=-500 \
    max=500 \
    type=jpg \
    scale=1,4,8,16 \
    out='| ppmlabel -color white -size {%0.75:5} -x 15 -y {%98} -text "SDO/HMI Quick-Look Magnetogram: {ID}" | pnmtojpeg -quality=95'

  @ day = $day + 1
  end

  if ($end_movie_t > 0) then
    set end_movie = `time_convert s=$end_movie_t zone=TAI`
    /home/phil/workflow/scripts/makeIcmovie $end_movie
  endif
