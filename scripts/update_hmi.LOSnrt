#
# Script to make HMI lev1.5 observables from HMI lev1 data
#
# this is run in the task instance directory where there is a 'ticket' file that specifies
# the request.

# XXXXXXXXXX test
# set echo
# XXXXXXXXXX test

set HERE = $cwd 

if ($?WORKFLOW_ROOT) then
  set WFDIR = $WORKFLOW_DATA
  set WFCODE = $WORKFLOW_ROOT
else
  echo Need WORKFLOW_ROOT variable to be set.
  exit 1
endif

foreach ATTR (WANTLOW WANTHIGH GATE)
   set ATTRTXT = `grep $ATTR ticket`
   set $ATTRTXT
end

set product = `cat $WFDIR/gates/$GATE/product`
set key = `cat $WFDIR/gates/$GATE/key`

# set HMIprogram = /home/couvidat/cvs/JSOC/bin/linux_x86_64/HMI_observables
set HMIprogram = $WFCODE/bin/HMI_observables
set MAKEIMAGES = "$WFCODE/scripts/makeimageslowhigh"

# note in Sebastien's code camera=1 is front camera, where CAMERA=2.
set camera = 1
set nrt = 1
set cadence = 45
set input = hmi.lev1_nrt
set otherargs = "levin="lev1" levout="lev15" wavelength=3"

# align times in format for HMIprogram

# round times to a slot
set indexlow = `index_convert ds=$product $key=$WANTLOW`
set indexhigh = `index_convert ds=$product $key=$WANTHIGH`
@ indexhigh = $indexhigh - 1
set wantlow = `index_convert ds=$product $key"_index"=$indexlow`
set wanthigh = `index_convert ds=$product $key"_index"=$indexhigh`
# make qsub name from index from wantlow
# set indexbase = `index_convert ds=$product $key=2010.03.24_TAI`
# @ qsubname = $indexhigh - $indexbase
# set timestr = `echo $wantlow  | sed -e 's/[._:]//g' -e 's/^.......//' -e 's/TAI//'`
set timestr = `echo $wantlow  | sed -e 's/[.:]//g' -e 's/^......//' -e 's/.._TAI//'`

set timename = NRT
set qsubname = $timename$timestr

if ($indexhigh < $indexlow) then
   echo No data to process, $WANTLOW to $WANTHIGH > $HERE/runlog
   exit 0
endif

set TEMPLOG = $HERE/runlog
set babble = $HERE/babble
set TEMPCMD = $HERE/$qsubname
echo 6 > $HERE/retstatus

# make qsub script
echo "#! /bin/csh -f " >$TEMPCMD
echo "cd $HERE" >>$TEMPCMD
echo "hostname >>&$TEMPLOG" >>$TEMPCMD
echo "$HMIprogram lev1=$input quicklook=$nrt camid=$camera cadence=$cadence $otherargs begin="$wantlow"  end="$wanthigh"  >>&$TEMPLOG" >>$TEMPCMD
echo 'set retstatus = $?' >>$TEMPCMD
echo 'echo $retstatus >' "$HERE/retstatus" >>$TEMPCMD
# echo 'if ($retstatus == 0) then' >>$TEMPCMD
# echo "  echo XXXX Start image processing" >>$TEMPCMD
# echo "  $MAKEIMAGES $wantlow $wanthigh" >>$TEMPCMD
# echo "endif" >> $TEMPCMD
echo "rm -f $HERE/qsub_running" >>$TEMPCMD

# execute qsub script
touch $HERE/qsub_running
qsub -e $TEMPLOG -o $TEMPLOG -q j8.q $TEMPCMD
endif

set waitcount=0
while (-e $HERE/qsub_running)
    sleep 10
    @ waitcount = $waitcount + 1
    if ($waitcount > 720) then
      echo "timed out after 2 hours" >$HERE/FailMessage
      set retstatus = 5
      exit $retstatus
    endif
    echo -n '+' >>$babble
end

set retstatus = `cat $HERE/retstatus`
exit $retstatus
