#! /bin/csh -f
# Make M_720s 1K fits files
# 

# set echo
# use NRT data.

set wantlow = $1
set wanthigh = $2

echo Make images for $wantlow to $wanthigh

# from here make script that can make _nrt fits files in standard place

# start with hourly files
set CADENCE = 60

set JSOC_REBIN = /home/jsoc/cvs/Development/JSOC/bin/linux_x86_64/jsoc_rebin
set JSOC_AS_FITS = /home/jsoc/cvs/Development/JSOC/bin/linux_x86_64/jsoc_export_as_fits
set TIME_CONVERT = /home/jsoc/cvs/Development/JSOC/bin/linux_x86_64/time_convert
set SHOW_INFO = /home/jsoc/cvs/Development/JSOC/bin/linux_x86_64/show_info

set obslist = (M)
set flaglist = (-c)

set finalimagetime = NONE

set isNRT = 1

set wantlow_t = `$TIME_CONVERT time=$wantlow`
set wanthigh_t = `$TIME_CONVERT time=$wanthigh`

@ daylow = $wantlow_t / 86400
@ dayhigh = $wanthigh_t / 86400

set day = $daylow
while ($day <= $dayhigh)
  @ day_t = 86400 * $day
  @ nextday_t = $day_t + 86400
  @ last_t = $nextday_t - 45
  if ($last_t > $wanthigh_t) set last_t = $wanthigh_t
  set first_t = $day_t
  if ($first_t < $wantlow_t) set first_t = $wantlow_t
  @ cadence = $CADENCE * 60
  @ first_mod_t = $first_t - 45
  @ imglow_t = $first_mod_t / $cadence
  @ imglow_t = $imglow_t + 1
  @ first_t = $imglow_t * $cadence
  set first = `$TIME_CONVERT s=$first_t zone=TAI`
  @ last_mod_t = $last_t + $cadence
  @ imghigh_t = $last_mod_t / $cadence
  @ n_images = $imghigh_t - $imglow_t
  if ($n_images < 0) set n_images = 0
  if ($n_images == 0) then
    @ day = $day + 1
    continue
  endif
  @ n_minutes = $n_images * $CADENCE
  set QRY = '['$first'/'$n_minutes'm@'$CADENCE'm]'
  set yyyymmdd = `echo $first | sed -e 's/_.*//' -e 's/\./Q/' -e 's/\./X/'`
  set YEAR = `echo $yyyymmdd | sed -e 's/Q.*//'`
  set MON = `echo $yyyymmdd | sed -e 's/^.*Q//' -e 's/X.*//'`
  set DAY = `echo $yyyymmdd | sed -e 's/^.*X//'`

  set IMGROOT = /home/jsoc/hmi/fits
  set IMGPATH = $IMGROOT/$YEAR
  if (!(-e $IMGPATH)) mkdir $IMGPATH
  set IMGPATH = $IMGPATH/$MON
  if (!(-e $IMGPATH)) mkdir $IMGPATH
  set IMGPATH = $IMGPATH/$DAY
  if (!(-e $IMGPATH)) mkdir $IMGPATH

  cd $IMGPATH

  set img = 1
  while ($img <= $#obslist)
    set obs = $obslist[$img]
    if ($obs == M ) then
        set in_nrt = hmi.M_720s_nrt
        set out_nrt = $in_nrt'_resized'
    else
        echo No code for $obs
        exit 1
    endif
    set n_nrt = `$SHOW_INFO -cq $in_nrt"$QRY"`
    set inseries = $in_nrt
    echo Use $inseries for "$QRY"

set echo
    $JSOC_REBIN $flaglist[$img] in=$inseries"$QRY" scale=0.25 out=$out_nrt
    $JSOC_AS_FITS \
        reqid='NA' \
        expversion=0.5 \
        rsquery=$out_nrt"$QRY" \
        n=0 \
        path=./ \
        ffmt='hmi.M_720s_nrt.{T_REC:A}.fits' \
        method='url' \
        protocol='fits' \
        cparms='compress Rice'
    @ img = $img + 1
    rm $IMGPATH/index.txt
  end

  @ day = $day + 1
end

set exitstatus = 0
set IMGROOT = /home/jsoc/hmi/fits
set LATESTLIST = $IMGROOT/latest_fits_time
set last_fits_t = `$SHOW_INFO -q hmi.M_720s_nrt_resized n=-1 key=t_obs`
@ s = `$TIME_CONVERT time=$last_fits_t` + 30
set real_t = `$TIME_CONVERT s=$s zone=tai`
set last_fits_y = `echo $real_t | awk -F\. '{print $1}'`
set last_fits_m = `echo $real_t | awk -F\. '{print $2}'`
set last_fits_d = `echo $real_t | awk -F\. '{print $3}' | awk -F\_ '{print $1}'`
set last_fits_h = `echo $real_t | awk -F\_ '{print $2}' | awk -F\: '{print $1}'`

set fitspath = http://jsoc.stanford.edu/data/hmi/fits/$last_fits_y/$last_fits_m/$last_fits_d

set tstamp = $last_fits_y$last_fits_m$last_fits_d'_'$last_fits_h'0000_TAI'
set last_file = "/home/jsoc/hmi/fits/$last_fits_y/$last_fits_m/$last_fits_d/hmi.M_720s_nrt.$tstamp.fits"
if ( -e $last_file ) then
  echo "M_1k_fits $fitspath//hmi.M_720s_nrt.$tstamp.fits" > $LATESTLIST
else
  echo $last_file > /tmp/fits_tstamp
  /usr/bin/Mail -s 'lastest Fits file missing' jeneen\@sun.stanford.edu < /tmp/fits_tstamp
endif

exit $exitstatus
