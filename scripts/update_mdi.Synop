#! /bin/csh -f

# Actiontask for Carrington synoptip charts from MDI fd_M longitude-sinelat remapped data.

# key type is CARROT so times are in Carrington rotation numbers.

# all rotations wantlow through wanthigh inclusive will be generated.

if ( $JSOC_MACHINE == "linux_x86_64" ) then
  set QUE = j.q
  set QSUB = qsub
else if ( $JSOC_MACHINE == "linux_avx" ) then
  set QUE = a.q
  set QSUB = /SGE2/bin/lx-amd64/qsub
endif


set MAGSYNOP = /home/phil/cvs/JSOC/bin/$JSOC_MACHINE/magsynop_jsoc
set CTIMES = /home/wso/bin/_linux4/ctimes

set wantlow = `cat wantlow`
set wanthigh = `cat wanthigh`

set HERE = $cwd
set LOG = $HERE/runlog
set CMD = $HERE/CR_$wantlow
set babble = $HERE/babble

date >>& $LOG

# make qsub script
echo "#! /bin/csh -f " >$CMD
echo "cd $HERE" >>$CMD
echo "hostname >>& $LOG" >>$CMD
echo "set nBAD = 0" >>$CMD

set crot = $wantlow
while ($crot <= $wanthigh)
  set lowT = `$CTIMES -j  "CT"$crot":360"`
  set highT = `$CTIMES -j "CT"$crot":0"`
  echo "$MAGSYNOP in='mdi.fd_M_96m_remap['$lowT'-'$highT']' out=mdi.synop CR=$crot checkqual=1 qualmask=0x402c01f2 nsig=3.0 >>& $LOG" >>$CMD
  echo 'set retstatus = $?' >>$CMD
  echo 'echo '$crot' done, status = $retstatus >>&LOG' >>$CMD
  echo 'if ($retstatus) @ nBAD = $nBAD + 1' >>$CMD
  @ crot = $crot + 1
end

echo 'echo $nBAD >retstatus' >>$CMD
echo "rm -f $HERE/qsub_running" >>$CMD

# execute qsub script
touch qsub_running
set LOG = `echo $LOG | sed "s/^\/auto//"`
$QSUB -e $LOG -o $LOG -q $QUE $CMD

set waitcount=0
while (-e qsub_running)
    sleep 10
    @ waitcount = $waitcount + 1
    if ($waitcount > 720) then
      echo "timed out after 2 hours" >FailMessage
      set retstatus = 5
      exit $retstatus
    endif
    echo -n '+' >>$babble
end

set retstatus = `cat $HERE/retstatus`
exit $retstatus
~                                                                                                     
