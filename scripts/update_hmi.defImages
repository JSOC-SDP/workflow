#! /bin/csh -f

set HERE = $cwd

if ($?WORKFLOW_ROOT) then
  set WFDIR = $WORKFLOW_ROOT
  set WFCODE = $WORKFLOW_ROOT
else
  echo Need WORKFLOW_ROOT variable to be set.
  exit 1
endif

if ( $JSOC_MACHINE == "linux_x86_64" ) then
  set QUE = j.q,o.q
  set QSUB = qsub
else if ( $JSOC_MACHINE == "linux_avx" ) then
  set QUE = a.q
  set QSUB = qsub2
endif

set wantlow = `cat wantlow`
set wanthigh = `cat wanthigh`

set TEMPLOG = $HERE/runlog
set timestr = `echo $wantlow  | sed -e 's/[._:]//g' -e 's/^......//' -e 's/..TAI//'`
set TEMPCMD = $HERE/"DIMG"$timestr
echo 0 > retstatus

# make qsub script
echo "#! /bin/csh -f " >$TEMPCMD
echo "cd $HERE" >>$TEMPCMD
echo "hostname >>&$TEMPLOG" >>$TEMPCMD
echo "$WFCODE/scripts/makeimageslowhigh $wantlow $wanthigh >>&$TEMPLOG"  >>$TEMPCMD
echo 'set retstatus = $?' >>$TEMPCMD
echo 'echo $retstatus >' "$HERE/retstatus" >>$TEMPCMD
echo "rm -f $HERE/qsub_running" >>$TEMPCMD

# execute qsub script
touch qsub_running
set TEMPLOG = `echo $TEMPLOG | sed "s/^\/auto//"`
$QSUB -sync yes -e $TEMPLOG -o $TEMPLOG -q $QUE $TEMPCMD
#qsub -sync yes -e $TEMPLOG -o $TEMPLOG -q j.q $TEMPCMD

if ( -e $HERE/qsub_running ) then
  rm -f $HERE/qsub_running
endif
if (-e retstatus) set retstatus = `cat $HERE/retstatus`
exit $retstatus

